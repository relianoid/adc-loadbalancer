#!/usr/bin/perl
###############################################################################
#
#    RELIANOID Software License
#    This file is part of the RELIANOID Load Balancer software package.
#
#    Copyright (C) 2014-today RELIANOID
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU Affero General Public License as
#    published by the Free Software Foundation, either version 3 of the
#    License, or any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU Affero General Public License for more details.
#
#    You should have received a copy of the GNU Affero General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
###############################################################################

use strict;
use warnings;
use feature qw(signatures);

use Relianoid::Log;
use Relianoid::Farm::Core;
use Relianoid::Farm::Base;
use Relianoid::Farm::Action;
use Relianoid::FarmGuardian;
use Relianoid::Net::Core;
use Relianoid::Net::Interface;
use Relianoid::Net::Route;
use Relianoid::Net::Util;
use Relianoid::Lock;

local $ENV{SCRIPT_NAME} = 'relianoid';
local $SIG{__WARN__}    = \&warning_signal;

my $eload = eval { require Relianoid::ELoad };

my $basedir           = &getGlobalConfiguration('basedir');
my $configdir         = &getGlobalConfiguration('configdir');
my $defaultgw         = &getGlobalConfiguration('defaultgw');
my $defaultgwif       = &getGlobalConfiguration('defaultgwif');
my $defaultgw6        = &getGlobalConfiguration('defaultgw6');
my $defaultgwif6      = &getGlobalConfiguration('defaultgwif6');
my $docker_flag       = &getGlobalConfiguration('docker_flag');
my $bin_dir           = &getGlobalConfiguration('bin_dir');
my $systemctl         = &getGlobalConfiguration('systemctl');
my $snmpd_srv         = &getGlobalConfiguration('snmpd_service');
my $ssh_srv           = &getGlobalConfiguration('sshService');
my $cherokee_srv      = &getGlobalConfiguration('http_server_service');
my $cron_service      = &getGlobalConfiguration('cron_service');
my $relianoid_service = &getGlobalConfiguration('relianoid_service');
my $action            = shift @ARGV // '';

# Read interfaces data blocks
sub getInterfaceBlocks (@file_lines) {
    my @interface_list = ();
    my @interface      = ();
    my $in_block       = undef;

    while (my $line = shift @file_lines) {
        next if $line =~ /^\s*$/;     # skip blank lines
        next if $line =~ /^\s*#/;     # skip commented lines
        next if $line =~ /^auto /;    # skip auto lines

        chomp $line;

        if ($line =~ /^iface /) {
            $in_block = 'true';

            if (@interface)    # exclude first block
            {
                my (@array) = @interface;
                push(@interface_list, \@array);    # store previous iface
            }

            @interface = ($line);                  # start new interface
        }
        elsif ($in_block) {
            push(@interface, $line);               # store previous iface
        }
    }

    # for last block found
    if (@interface) {
        push(@interface_list, \@interface);        # store last iface
    }

    return @interface_list;
}

sub local_http_service ($command) {
    if (!grep { $command eq $_ } qw(start stop restart)) {
        die "local_http_service '$command' command not recognized";
    }

    &logAndRunBG("$systemctl $command $cherokee_srv");
    return;
}

sub import_system_network_interfaces () {
    # import system interface to RELIANOID if configured
    #testing interface configured on the installation and gw

    my $etc_network_interfaces = "/etc/network/interfaces";

    if (!-e $etc_network_interfaces) {
        return;
    }

    my @file = ();

    if (open my $fr, '<', $etc_network_interfaces) {
        @file = <$fr>;
        close $fr;
    }
    else {
        my $msg = "Could not open file $etc_network_interfaces: $!";
        log_error($msg);
    }

    if (grep { /relianoid_modified/ } @file) {
        return;
    }

    my @blocks = &getInterfaceBlocks(@file);

    for my $block (@blocks) {
        my ($ifname, $ip, $netmask, $gateway);

        for my $line (@{$block}) {
            chomp($line);

            if ($line =~ /iface.*inet static/i) {
                (undef, $ifname) = split(" ", $line);
            }
            elsif ($line =~ /\Waddress/i) {
                my (undef, $ip_n_mask) = split(" ", $line);
                my $mask;
                ($ip, $mask) = split("/", $ip_n_mask);

                if ($mask)    # only when bitmask is declared
                {
                    require Net::Netmask;
                    my $block = Net::Netmask->new2($ip_n_mask);

                    if ($block) {
                        $netmask = $block->mask;
                    }
                    else {
                        &log_info($Net::Netmask::error);
                        die $Net::Netmask::error;
                    }
                }
            }
            elsif ($line =~ /netmask/i) {
                (undef, $netmask) = split(" ", $line);
            }
            elsif ($line =~ /gateway/i) {
                (undef, $gateway) = split(" ", $line);
            }
        }

        my %interface = (
            name    => $ifname,
            addr    => $ip,
            mask    => $netmask,
            gateway => $gateway,
            status  => "up",
        );

        # Write interface file
        if ($interface{name}) {
            &log_info("Writing interface: $interface{name}");
            &setInterfaceConfig(\%interface);

            # gateway
            if ($interface{gateway}) {
                &log_info("Writing gateway: $interface{gateway}");
                setGlobalConfiguration('defaultgw',   $gateway);
                setGlobalConfiguration('defaultgwif', $ifname);
            }

            # route table
            &log_info("Writing route table: $interface{name}");
            &writeRoutes($interface{name});
        }
    }

    # overwriting interfaces file
    my $file_content =            #
      "# relianoid_modified\n"    #
      . "auto lo\n"               #
      . "iface lo inet loopback\n";

    require Relianoid::File;
    setFile($etc_network_interfaces, $file_content);

    return;
}

if ($action eq "start") {
    # Configure NIC interfaces in docker.
    if (-f $docker_flag) {
        &logAndRun("${bin_dir}/check-nic-dhcp");
    }

    # ZNA firmware upgrade
    my $migration_flag = &getGlobalConfiguration('migration_flag', 1);
    if (-e $migration_flag) {
        if ($eload) {
            &eload(module => 'Relianoid::EE::Hw', func => 'setHwVersion');
        }

        # Migrating config files
        &migrateConfigFiles();
        my $rm = &getGlobalConfiguration('rm');
        &logAndRun("$rm $migration_flag");
    }

    my $version = &getGlobalConfiguration('version');
    &log_info("Starting RELIANOID Load Balancer v${version}");

    if ($eload) {
        &eload(module => 'Relianoid::EE::Service', func => 'service_cert_message');
    }

    # Load required modules
    &log_info("Loading Layer 4 modules");
    require Relianoid::Farm::L4xNAT::Service;
    &loadL4FarmModules();

    import_system_network_interfaces();

    if ($eload) {
        &eload(module => 'Relianoid::EE::Service', func => 'start_service');
    }

    log_info("Starting network interfaces...");

    my $ip_bin            = &getGlobalConfiguration('ip_bin');
    my @interfaces        = @{ &getConfigInterfaceList() };
    my @system_interfaces = &getInterfaceList();

    &log_info("Setting up NIC interfaces...");

    require Relianoid::Net::Core;
    require Relianoid::VPN::Core;

    # first real interfaces
    for my $iface (@interfaces) {
        if ($eload) {
            next if (defined $iface->{type} and $iface->{type} eq 'bond');
            next unless getVpnExists($$iface{name});
        }

        # interfaces as eth0 for example
        if ($$iface{name} eq $$iface{dev}) {
            # check if it is an existent device
            if (!grep { $$iface{name} eq $_ } @system_interfaces) {
                &log_info("Nonexistent NIC interface $$iface{name} Config found!.");

                # skip ethX types, ethA1 for example
                next if ($$iface{name} =~ /eth(?:[a-z]|[A-Z])+/);

                # remove interface config
                my $if_file = &getInterfaceConfigFile($$iface{name});
                if (-f $if_file) {
                    &log_info("Deleting Nonexistent NIC interface Config File $if_file.");
                    unlink $if_file;
                }

                # remove rt_tables entry
                require Relianoid::Net::Route;
                &deleteRoutesTable($$iface{name});

                # remove routing config
                if ($eload) {
                    eload(module => 'Relianoid::EE::Net::Routing', func => 'delRoutingDependIface', args => [ $$iface{name} ]);
                }

                # remove gw entry
                if ($$iface{name} eq &getGlobalConfiguration('defaultgwif')) {
                    &setGlobalConfiguration('defaultgw',   '');
                    &setGlobalConfiguration('defaultgwif', '');
                }
                elsif ($$iface{name} eq &getGlobalConfiguration('defaultgwif6')) {
                    &setGlobalConfiguration('defaultgw6',   '');
                    &setGlobalConfiguration('defaultgwif6', '');
                }

                next;
            }

            use IO::Interface ':flags';

            if ($$iface{status} eq "up") {
                my $log_msg = "Starting ethernet: $$iface{name}";
                &upIf($iface);

                if (exists $$iface{addr} and length $$iface{addr}) {
                    $log_msg .= ", address: $$iface{addr}/$$iface{mask}";

                    if (defined $$iface{gateway} and $$iface{gateway} ne '') {
                        $log_msg .= ", gateway: $$iface{gateway}";
                    }

                    my $errno = &addIp($iface);

                    if ($errno) {
                        my @ip_output = @{ &logAndGet("$ip_bin address show dev $$iface{name}", "array") };

                        if (grep { /$$iface{addr}/ } @ip_output) {
                            $errno = 0;
                        }
                    }

                    # kept in case it is required for first interface
                    &writeRoutes($$iface{name});
                    &applyRoutes("local", $iface);

                    $errno ? log_error($log_msg) : log_info($log_msg);

                    if (defined $$iface{ip_v} and $$iface{ip_v} == 4) {
                        &sendGPing($$iface{name});
                    }
                }
                else {
                    log_info($log_msg);
                }
            }
        }
    }

    # bugfix, force the start of the ssh and http services
    &logAndRun("$systemctl start $ssh_srv");
    &logAndRun("$systemctl start $cherokee_srv");

    &log_info("Setting up VLAN interfaces...");

    # vlans interfaces (ie. eth0.20)
    for my $iface (@interfaces) {
        if ($$iface{vlan} and not defined $$iface{vini}) {
            unless (scalar grep { $$iface{name} eq $_ } &getLinkNameList()) {
                &createIf($iface);
            }

            if ($eload) {
                eload(module => 'Relianoid::EE::Net::Mac', func => 'addMAC', args => [ $iface->{name}, $iface->{mac} ]);
            }

            my $errno = &addIp($iface);

            if ($$iface{status} eq "up") {
                my $log_msg = "Starting VLAN: $$iface{name}, address: $$iface{addr}/$$iface{mask}";

                if ($$iface{gateway}) {
                    $log_msg .= ", gateway: $$iface{gateway}";
                }

                &upIf($iface);

                if ($errno) {
                    my @ip_output = @{ &logAndGet("$ip_bin address show dev $$iface{name}", "array") };
                    if (grep { /$$iface{addr}/ } @ip_output) {
                        $errno = 0;
                    }
                }

                $errno ? log_error($log_msg) : log_info($log_msg);

                &applyRoutes("local", $iface);

                if ($$iface{ip_v} == 4) {
                    &sendGPing($$iface{name});
                }
            }
        }
    }

    if ($eload) {
        &eload(module => 'Relianoid::EE::Service', func => 'start_cluster_and_modules');
    }

    &log_info("Relianoid service: Starting VPN...", "VPN");
    require Relianoid::VPN::Action;
    &initVPNModule();

    &log_info("Setting up virtual interfaces...");

    # virtual interfaces (ie. eth0:20, eth0.20:2)
    for my $iface (@interfaces) {
        next unless defined $$iface{vini};
        next unless $$iface{status} eq "up";

        my $log_msg = "Starting virtual interface: $$iface{name}, address: $$iface{addr}/$$iface{mask}";
        my $errno   = &addIp($iface);

        if ($errno) {
            my $toif = &getParentInterfaceName($$iface{name});
            &log_error("Error at addIp for $$iface{name}");
            my @ip_output = @{ &logAndGet("$ip_bin -$$iface{ip_v} address list dev $toif", "array") };

            if (grep { /$$iface{addr}/ } @ip_output) {
                $errno = 0;
            }
        }

        &applyRoutes("local", $iface);

        if ($eload) {
            &eload(module => 'Relianoid::EE::Net::Routing', func => 'applyRoutingDependIfaceVirt', args => [ 'add', $iface ]);
        }

        $errno ? log_error($log_msg) : log_info($log_msg);

        if ($$iface{ip_v} == 4) {
            my ($parent_if) = split(":", $$iface{name});
            &sendGPing($parent_if);
        }
    }

    &configureDefaultGW();

    require Relianoid::VPN::Action;
    start_vpn_service();

    if (not $eload or not &eload(module => 'Relianoid::EE::Service', func => 'service_start_farms')) {
        log_info("Starting farms...");
        &getGlobalConfiguration(undef, 1);

        my @farmsf    = &getFarmList();
        my $enable_fg = 1;

        $enable_fg = &eload(module => 'Relianoid::EE::Service', func => 'getEnableFarmGuardian') if $eload;

        log_info("Setting up farms...");

        for my $ffile (@farmsf) {
            my $farmname = &getFarmName($ffile);
            my $bstatus  = &getFarmBootStatus($farmname);

            if ($bstatus eq "up") {
                my $errno = &runFarmStart($farmname, "false");
                my $msg   = "Starting farm: $farmname";

                $errno ? log_error($msg) : log_info($msg);

                #farmguardian configured and up?
                if ($errno == 0) {
                    my $errno = &runFGFarmStart($farmname);
                    my $msg   = "Starting FarmGuardian for: $farmname";

                    $errno ? log_error($msg) : log_info($msg);
                }
            }
            else {
                log_info("Farm: $farmname configured DOWN");
            }
        }
    }

    # Reload the notifications cron file
    if ($eload) {
        &eload(module => "Relianoid::EE::Notify", func => "schedule_notifications");
    }

    # At this point every relianoid interface and farm is running
    # now a personalized script can be executed
    my $start_script = &getGlobalConfiguration('start_script');

    &log_info("Running $start_script");
    my @ownscript = `$start_script 2>&1`;
    &log_info(@ownscript) if (@ownscript);
    &log_info("End of $start_script");

    # make sure related system services are running
    if (my $errno = &logAndRunCheck("$systemctl --quiet is-active $ssh_srv")) {
        &logAndRun("$systemctl start $ssh_srv");
    }

    my $snmpdstatus = &logAndGet("$systemctl is-enabled $snmpd_srv");
    if ($snmpdstatus eq 'enabled') {
        # Bugfix: SNMP didn't start if the IP is not up
        &logAndRun("$systemctl restart $snmpd_srv");
    }

    &local_http_service('start');

    # Check the failback configuration
    if ($eload) {
        &eload(module => 'Relianoid::EE::Service', func => 'check_cluster_failback_conf');
    }

    &log_info("Start process finished");
}
elsif ($action eq "stop") {

    &log_info("Stopping RELIANOID Load Balancer");

    if ($eload) {
        &eload(module => 'Relianoid::EE::Service', func => 'service_cert_message');
        &eload(module => 'Relianoid::EE::Service', func => 'stop_service');
    }

    log_info("Stopping farms...");

    for my $ffile (getFarmList()) {
        my $farmname = &getFarmName($ffile);
        my $status   = &getFarmStatus($farmname);

        if ($status eq "up") {
            #farmguardian configured and up?
            my $fgstatus = &getFarmGuardianPid($farmname);

            if ($fgstatus != -1) {
                my $errno = &runFarmGuardianStop($farmname);
                my $msg   = "Stopping FarmGuardian for: $farmname";

                $errno ? log_error($msg) : log_info($msg);
            }

            my $errno = &runFarmStop($farmname);
            my $msg   = "Stopping farm: $farmname";

            $errno ? log_error($msg) : log_info($msg);
        }
        elsif ($status eq "down") {
            if (&getFarmType($farmname) =~ "^http") {
                use Relianoid::Farm::HTTP::Action;
                &checkFarmHTTPSystemStatus($farmname, "down", "true");
            }
        }
    }

    log_info("Stopping nftlb service...");

    &logAndRun("$systemctl stop nftlb");

    my $status  = &logAndGet("$systemctl status nftlb");
    my $active  = system("$systemctl is-active --quiet nftlb");
    my $msg     = "Stopping service: nftlb";

    $active != 0 ? log_info($msg) : log_error($msg);

    require Relianoid::VPN::Action;
    stop_vpn_service();

    log_info("Stopping virtual interfaces...");

    # Virtual Network Interfaces
    for my $iface (@{ &getConfigInterfaceList() }) {
        if (defined $$iface{vini}) {
            my $log_msg = "Stopping virtual interface: $$iface{name}, address: $$iface{addr}/$$iface{mask}";
            my $errno   = &stopIf($iface);

            $errno ? log_error($log_msg) : log_info($log_msg);
        }
    }

    if ($eload) {
        &eload(module => 'Relianoid::EE::Service', func => 'disable_cluster');
    }

    &configureDefaultGW();

    # At this point every relianoid interface and farm is stopped
    # now a personalized script can be executed
    my $stop_script = &getGlobalConfiguration('stop_script');

    &log_info("Running $stop_script");
    my @ownscript = `$stop_script 2>&1`;
    &log_info(@ownscript) if @ownscript;
    &log_info("End of $stop_script");

    &log_info("Stop process finished");
}
else {
    print "Usage: relianoid {start|stop}\n";
    exit 1;
}
